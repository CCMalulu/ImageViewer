#setting title, Enhance
#setting description, Local contrast and saturation enhancements as well as global saturation.

#param Radius, 2, int, 10, 3, 20
#param Brightness Contrast, 3, float, 1.0, 0.0, 10.0
#param Color Contrast, 4, float, 1.0, 0.0, 10.0
#param Saturation, 5, float, 1.0, 0.0, 10.0

layout(location = 2) uniform int RADIUS;
layout(location = 3) uniform float BR_CONTRAST;
layout(location = 4) uniform float COLOR_CONTRAST;
layout(location = 5) uniform float SATURATION;

// Simple Gauss-Kernel. Normalization is not included and must be
// done by dividing through the weight sum.
float kernel(float _offset, float _variance)
{
	return exp(- _offset * _offset / _variance);
}

vec3 readPixel(ivec2 pos)
{
	pos = clamp(pos, ivec2(0), textureSize(src_image, 0).xy - 1);
	return texelFetch(src_image, pos, 0).rgb;
}

void main()
{
	ivec2 pixelCoord = ivec2(gl_GlobalInvocationID.xy) + pixelOffset;
	
	vec2 avgBrSat = vec2(0.0);
	float weightSum = 0.0;
	vec3 centerColor = readPixel(pixelCoord);

	// Decompose center color
	float centerBr = dot(centerColor, vec3(0.299, 0.587, 0.114)) + 1e-6;
	vec2 ab = vec2(centerColor.r - 0.5 * (centerColor.g + centerColor.b),
					0.866025404 * (centerColor.g - centerColor.b));
	float centerSat = length(ab) / centerBr;
	//float centerH = atan(b, a);
	if(centerBr != centerBr) {
		centerBr = 0.5;
		centerSat = 0.5;
		ab = vec2(0.0);
	}

	// Gaussian filter to get the average brightness and saturation
	for(int y = -RADIUS; y <= RADIUS; y++)
	for(int x = -RADIUS; x <= RADIUS; x++)
	{
		vec3 color = readPixel(pixelCoord + ivec2(x,y));
		float brightness = dot(color, vec3(0.299, 0.587, 0.114)) + 1e-6;
		if(brightness != brightness) continue;
		float saturation = length(vec2(color.r - 0.5 * (color.g + color.b), 0.866025404 * (color.g - color.b))) / brightness;
		float d = sqrt(x*x + y*y);
		float w = kernel(d, RADIUS * RADIUS / 4.0) * kernel(brightness - centerBr, 0.5);
		weightSum += w;
		avgBrSat += w * vec2(brightness, saturation);
	}
	avgBrSat /= weightSum;

	// Increase contrasts
	float newSat = avgBrSat.y + (centerSat - avgBrSat.y) * COLOR_CONTRAST;
	newSat = newSat * SATURATION;
	float newBr = avgBrSat.x + (centerBr - avgBrSat.x) * BR_CONTRAST;

	// Return to RGB
	ab *= newSat * newBr / (centerSat * centerBr + 1e-6);
	vec3 outColor = newBr + vec3(0.701 * ab.x - 0.273086677 * ab.y,
								-0.299 * ab.x + 0.304263592 * ab.y,
								-0.299 * ab.x - 0.850436947 * ab.y);

	imageStore(dst_image, pixelCoord, vec4(outColor, 1.0));
}